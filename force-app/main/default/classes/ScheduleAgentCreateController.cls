
/**
     * @description This class is used to create Session Slot records for Agentforce.
     *              It provides an invocable method that takes a list of SessionSlotArray objects
     *              and inserts the Session Slot records into the database.
     * @Version 1.0
     */
public with sharing class ScheduleAgentCreateController {

    /**
    * @description This method is used to create Session Slot records for Agentforce.
    * It takes a list of SessionSlotArray objects as input and inserts the Session Slotrecords into the database.
    * @Version 1.0
    * @param input InvocableMethods called by Agentforce must have an input parameter, we don't actually need any kind of input for this method so it's value is not used.
    * @return No return value is needed, but a return is required by the InvocableMethod annotation.
    * @throws None
    */
    @InvocableMethod(
        label='Insert Session Slot records to complete scheduling'
        description='This will create the session slot records that have been generated by the scheduler'
    )
    public static List < SessionSlotArray > getSessionsToSchedule(List < SessionSlotArray > input) {
        System.debug('Input: ' + JSON.serialize(input));

        List < Session_Slot__c > sessionSlots = new List < Session_Slot__c > ();
        for (SessionSlotArray sessionSlotArray: input) {
            for (SessionSlot sessionSlot: sessionSlotArray.sessionSlotArray) {
                Session_Slot__c ss = new Session_Slot__c();
                ss.Session__c = sessionSlot.sessionId;
                ss.Session_Location__c = sessionSlot.locationId;
                ss.Start_Time__c = sessionSlot.startTime;
                ss.End_Time__c = sessionSlot.endTime;
                ss.Date__c = sessionSlot.sessionDate;
                sessionSlots.add(ss);
            }
        }

        insert sessionSlots;

        return input;

    }

    public class SessionSlotArray {
        @InvocableVariable(
            description='Session Slot Array'
        )
        public List < SessionSlot > sessionSlotArray;

        public SessionSlotArray() {}

        public SessionSlotArray(List < SessionSlot > sessionSlotArray) {
            this.sessionSlotArray = sessionSlotArray;
        }
    }


    public class SessionSlot {
        public Id sessionSlotId;
        public Id sessionId;
        public Id locationId;
        public Time startTime;
        public Time endTime;
        public Date sessionDate;

        public SessionSlot() {}

        public SessionSlot(Id sessionId, Id locationId, Time startTime, Time endTime, Date dt) {
            this.sessionId = sessionId;
            this.locationId = locationId;
            this.startTime = startTime;
            this.endTime = endTime;
            this.sessionDate = dt;
        }
    }
}